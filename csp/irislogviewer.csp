<csp:class description="View Console Log">
<html>
<head>

	<title>	Console Log Viewer </title>
	<!-- Required meta tags -->
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">

    <!-- Bootstrap CSS -->
    <link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.4.1/css/bootstrap.min.css" integrity="sha384-Vkoo8x4CGsO3+Hhxv8T/Q5PaXtkKtu6ug5TOeNV6gBiFeWPGFN9MuhOf23Q9Ifjh" crossorigin="anonymous">
    
	<script src="https://code.jquery.com/jquery-3.4.1.min.js" crossorigin="anonymous"></script>
	<script src="https://cdn.jsdelivr.net/npm/popper.js@1.16.0/dist/umd/popper.min.js" integrity="sha384-Q6E9RHvbIyZFJoft+2mJbHaEWldlvI9IOYy5n3zV9zzTtmI3UksdQRVvoxMfooAo" crossorigin="anonymous"></script>
	<script src="https://stackpath.bootstrapcdn.com/bootstrap/4.4.1/js/bootstrap.min.js" integrity="sha384-wfSDF2E50Y2D1uUdj0O3uMBJnjuUD4Ih7YwaYd1iqfktj0Uod8GCExl3Og8ifwB6" crossorigin="anonymous"></script>

</head>

<body>
<script language="Cache" runat="server">
	Do ..DisplayLog()
</script>

</body>

<script language="Cache" method="DebugText" arguments="pText">
	Set ^TESTlog($I(^TESTlog)) = $ZDateTime($NOW(),8,1,3)_" : "_$JOB_" : "_pText
	Quit
</script>

<script language="Cache" method="DisplayLog" arguments="">
	New pDebug
	Set pDebug = 0
	Set pImportDeleteExtent = 4  // I want to Delete Extent ONCE
	Set iAlerts = 0
	Set iFormat = "DISPLAY"
	If ($data(%request.Data("iAlerts",1))) {
	 	Set iAlerts = %request.Data("iAlerts",1)
	}
	If pDebug Do ..DebugText(iAlerts)
	If ($data(%request.Data("iFormat",1))) {
	 	Set iFormat = %request.Data("iFormat",1)
	}
	If ($data(%request.Data("iFromPeriod",1))) {
	 	Set iFromPeriod = %request.Data("iFromPeriod",1)
	 	Set pFilter("iFromPeriod") = iFromPeriod
	}
	If ($data(%request.Data("iMaxSeverity",1))) {
	 	Set iMaxSeverity = %request.Data("iMaxSeverity",1)
	 	Set pFilter("iMaxSeverity") = iMaxSeverity
	}
	If ($data(%request.Data("iMinSeverity",1))) {
	 	Set iMinSeverity = %request.Data("iMinSeverity",1)
	 	Set pFilter("iMinSeverity") = iMinSeverity
	}
	If ($data(%request.Data("iToPeriod",1))) {
	 	Set iToPeriod = %request.Data("iToPeriod",1)
	 	Set pFilter("iToPeriod") = iToPeriod
	}
	If $get(%request.CgiEnvs("SERVER_NAME"))'="" {
		Set url="//"_%request.CgiEnvs("SERVER_NAME")_%request.URL
	} Else {
		Set url=%request.URL
	}
	Set dir = $$GetFilename^%apiCSP(url)
	If $$$isWINDOWS Set dir = $translate(dir,"\","/")
 	If $$$isVMS {
 		Set dir = $piece(dir,"]")_"]"
 	} Else {
 		Set dir = $piece(dir,"/",1,$length(dir,"/")-1)
 	}
	Set mgrdir = $system.Util.ManagerDirectory()  // $zu(12)
	Set alerts = mgrdir_"alerts.log"
	If pDebug Do ..DebugText(alerts)
	Set source = mgrdir_"messages.log"
	Set target = dir_"/download_messages.txt"
	Set pDeleteBeforeCopy = 1
	Set tSC = ##class(otw.log.Log).CopyFile(source,target,pDeleteBeforeCopy,.return)
	If ($Get(iAlerts) = "true") {
		Set tAlertsChecked = " checked"
	}
	&html<
		<P><B>Date/Time:</B>
		<label for="fromperiod">From</label>
		<input type="datetime-local" id="fromperiod" name="from" value="#($Get(iFromPeriod))#">
		<label for="toperiod">to</label>
		<input type="datetime-local" id="toperiod" name="to" value="#($Get(iToPeriod))#">
		<B>Levels:</B>
		<label for="fromlevel">From</label>
		<input type="text" id="fromlevel" name="from" value="#($Get(iMinSeverity))#">
		<label for="tolevel">to</label>
		<input type="text" id="tolevel" name="to" value="#($Get(iMaxSeverity))#">
		<a href="irislogviewer.csp">Clear filters</a>
		<P><B>#(dir)#</B>
		<input type="checkbox" id="checkAlerts" name="checkAlerts" value="checkAlerts"#($Get(tAlertsChecked))#>
		<label for="checkAlerts">Show Alerts</label>
		<a href="download_messages.txt" download="messages.log" target="_blank">Download link</a>
 	>
	&html<<button type="button" class="btn btn-primary" onclick="goClickButton('DISPLAY')">Display</button>>
	&html<<button type="button" class="btn btn-dark" onclick="goClickButton('RAW')">Raw</button>>
	&html<<hr>>
	//
	If ($Get(iAlerts) = "true") {
	&html<<table cellpadding=0 border=1 style="color:red;">>
	Set pImportDeleteExtent = $Get(pImportDeleteExtent,4)  // Delete Extent only ONCE
	Set pImportAlerts = 2
	Set pImport = pImportDeleteExtent + pImportAlerts
	Set pMaxLines = -1
	Set pFilter("pMaxLines") = pMaxLines
	Set tSC = ##class(otw.log.Log).ImportMessages(alerts,.pAlertLines,.pFilter,pImport)
	If pDebug Do ..DebugText(pAlertLines)
	If pAlertLines {
		Set tSC = ##class(otw.log.Log).OutputAlerts(alerts,iFormat,.pAlertLines)
	}
	Set pImportDeleteExtent = 0  // Delete Extent already happened above
	//
 	&html<
	</table>
	<hr>>
	}
	If ($Get(iFormat,"DISPLAY") = "DISPLAY") {
		&html<<table class="table table-hover table-bordered">>
		Set pFile = ""
		Set pImport = pImportDeleteExtent + 1  // I want to Delete Extent ONCE
		Set pMaxLines = -1
		Set pFilter("pMaxLines") = pMaxLines
		Set tSC = ##class(otw.log.Log).ImportMessages(pFile,.pLines,.pFilter,pImport)
		Set tSC = ##class(otw.log.Log).OutputTableData()
	}
	If ($Get(iFormat,"DISPLAY") = "RAW") {
		&html<<table cellpadding=0 border=1>>
		Set pFile = ""
		Set pImport = 0
		Set pMaxLines = -1
		Set pFilter("pMaxLines") = pMaxLines
		Set tSC = ##class(otw.log.Log).ReadLogLines(pFile,.pLines,.pFilter,pImport)
		Set tSC = ##class(otw.log.Log).OutputRawData(.pLines)
	}
	&html<
	</table>
	<hr>>
	Quit
</script>
<script language="JavaScript">
function goClickButton(id)
{
	if (id == 'MENU') {
		document.location = "menu.csp";
		return;
	}
	var iAlerts = document.getElementById("checkAlerts").checked;  // true or false
	var iFormat = "DISPLAY";
	if (id == 'RAW') {
		iFormat = "RAW";
	}
	var iFromPeriod = document.getElementById("fromperiod").value;
	var iToPeriod = document.getElementById("toperiod").value;
	var iMinSeverity = document.getElementById("fromlevel").value;
	var iMaxSeverity = document.getElementById("tolevel").value;
	var params1 = "&iFormat=" + iFormat + "&iFromPeriod=" + iFromPeriod + "&iToPeriod=" + iToPeriod;
	var params2 = "&iMinSeverity=" + iMinSeverity + "&iMaxSeverity=" + iMaxSeverity;
	var params = "iAlerts=" + iAlerts + params1 + params2;
	document.location = "irislogviewer.csp?" + params
}
</script>

</html>
